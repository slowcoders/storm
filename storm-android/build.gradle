apply plugin: 'java-library'

repositories {
    jcenter()
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'sqldroid/src/main/java',
                    '../storm/src/main/java'
            ]
        }
    }

    test {
        java {
            srcDirs = [
                    'src/test/gen',
                    'src/test/java'
            ]
        }
    }
}

ext {
    compileSdk = 26

    Properties properties = new Properties();
    properties.load(project.file('local.properties').newDataInputStream())

    androidJar = properties.getProperty('sdk.dir') + '/platforms/android-' + compileSdk + '/android.jar'
}

dependencies {
    compileOnly files("${androidJar}")

    implementation 'org.slowcoders:serialize:1.0.0'

    implementation group: 'com.google.guava', name: 'guava', version: '25.1-android'

    // libraries for test
    testCompile fileTree(dir : 'libs', include : '*.jar')

    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
}

bintray {
    user = rootProject.findProperty('maven.username')
    key = rootProject.findProperty('maven.password')
    publications = ['publication']

    publish = true

    pkg {
        repo = 'slowcoders'
        name = 'storm-android'
        vcsUrl = 'https://github.com/slowcoders/storm.git'
        version {
            name = rootProject.version.toString()
        }
    }
}

publishing {
    publications {
        publication(MavenPublication) {
            from components.java
            groupId 'org.slowcoders'
            artifactId 'storm-android'
            version rootProject.version.toString()
            artifact sourcesJar
        }


    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-parameters"]
    options.encoding = 'UTF-8'
}

