buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'org.slowcoders'
version '0.7.0'

allprojects {
    repositories {
        jcenter()
    }
}

bintray {
    user = findProperty('maven.username')
    key = findProperty('maven.password')
    publications = ['publication']

    publish = true

    pkg {
        repo = 'slowcoders'
        name = 'storm'
        vcsUrl = 'https://github.com/slowcoders/storm.git'
        version {
            name = project.version
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        publication(MavenPublication) {
            from components.java
            groupId 'org.slowcoders'
            artifactId 'storm'
            version project.version
            artifact sourcesJar
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
            ]
        }
    }

    test {
        java {
            srcDirs = [
                    'src/test/gen',
                    'src/test/java'
            ]
        }
    }
}

ext {
    targetPlatform = 'java'
}


dependencies {
    implementation 'org.slowcoders:serialize:1.0.0'

    if (targetPlatform == 'java') {
        implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.27.2.1'
    }

    // libraries for test
    testCompile fileTree(dir : 'libs', include : '*.jar')

    testCompile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-parameters"]
    options.encoding = 'UTF-8'
}

